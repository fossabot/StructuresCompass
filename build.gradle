import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.0.+', changing: true
    }
}

plugins {
    id 'eclipse'
    id 'com.geoffgranum.gradle-conventional-changelog' version '0.3.1'
}
apply plugin: 'net.minecraftforge.gradle'

def baseVersion = '1.0.2'
def minecraftVersion = '1.12.2'
version = "${minecraftVersion}-${baseVersion}" as Object
group = 'com.github.samarium150'
archivesBaseName = 'structurescompass'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

logger.quiet('Java: {} JVM: {}({}) Arch: {}',
    System.getProperty('java.version'),
    System.getProperty('java.vm.version'),
    System.getProperty('java.vendor'),
    System.getProperty('os.arch')
)

sourceSets.main.java { srcDir 'src/main/java' }

processResources {
    inputs.property "version", baseVersion
    inputs.property "mcversion", minecraftVersion
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include 'mcmod.info'
        expand 'version': baseVersion, 'mcversion': minecraftVersion
    }
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude 'mcmod.info'
    }
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                structurescompass {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                structurescompass {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
// sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
}

tasks.named("jar").configure {
    manifest {
        attributes([
            "Specification-Title": "structurescompass",
            "Specification-Vendor": "Samarium, SnightW, LinHeLurking",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"Samarium, SnightW, LinHeLurking",
            "Implementation-Timestamp": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
        ])
    }
}
jar.finalizedBy('reobfJar')

changelog {
    appName "${project.name}"
    versionNum "${project.version}"
    repoUrl "https://github.com/Samarium150/StructuresCompass"
    trackerUrl "https://github.com/Samarium150/StructuresCompass"
}

javadoc {
    options.locale = "en_US"
    options.encoding = "UTF-8"
}
